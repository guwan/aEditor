buildscript {
	ext {
		springBootVersion = '1.3.1.RELEASE'
		gradleNodeVersion = '0.11'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://jcenter.bintray.com/"
			//url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
		classpath "com.moowork.gradle:gradle-node-plugin:${gradleNodeVersion}"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot' 
apply plugin: 'com.moowork.node'

node {
    version = '4.2.4'
    npmVersion = '2.5.1'
    download = true
}

jar {
	baseName = 'aEditor'
	version = '0.0.1-SNAPSHOT'
  from 'dist'
  eachFile { details ->
      details.path = details.path.startsWith('META-INF') ?: 'static/'+details.path
  }
  // Jar contains duplicate empty folders, see Gradle issue:
  // http://issues.gradle.org/browse/GRADLE-1830
  // so we need to set includeEmptyDirs to false
  includeEmptyDirs = false
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	//maven {
	//     url "http://repo1.maven.org/maven2/"
	//}
	maven {
	     url "https://repo1.gradle.org/m2/"
	}
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	runtime('com.h2database:h2')
	testCompile('org.springframework.boot:spring-boot-starter-test') 
}


eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}

task npmBuild(dependsOn: npmInstall, type: NpmTask) {
    inputs.dir "src"
    inputs.file "gulpfile.js"
    outputs.dir "dist"
    args = ['run', 'build']
}

jar.dependsOn npmBuild
bootRun.dependsOn npmBuild